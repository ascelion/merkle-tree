
def publishName = 'merkle-tree'

apply plugin: 'maven-publish'

def doPublish  = project.hasProperty( 'doPublish' )
def isSnapshot = version.endsWith( '-SNAPSHOT' )
def isRelease  = !isSnapshot

def signKey    = "${publishName}.sign.keyId"

def gext       = gradle.ext

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
	from javadoc
	archiveClassifier = 'javadoc'
}

javadoc {
	failOnError = true

	options {
		memberLevel = 'PROTECTED'
		overview = file( 'src/main/java/overview.html' )
		links += [ 'https://docs.oracle.com/javase/8/docs/api' ]
	}
}

publishing {
	publications {
		maven( MavenPublication ) {
			from components.java

			artifact sourcesJar
			artifact javadocJar

			pom {
				name = artifactId
				url = 'https://github.com/ascelion/merkle-tree'
				description = 'Merkle Tree - A generic implementation of Merkle trees'
				scm {
					url = 'https://github.com/ascelion/merkle-tree'
					connection = 'https://github.com/ascelion/merkle-tree.git'
					developerConnection = 'git@github.com:ascelion/merkle-tree.git'
				}
				licenses {
					license {
						name = 'GNU Affero General Public License version 3'
						url = 'https://opensource.org/licenses/AGPL-3.0'
						distribution = 'repo'
					}
				}
			}
		}
	}

}

if( gradle.hasProperty( signKey ) ) {
	apply plugin: 'signing'

	ext['signing.gnupg.keyName'] = gext[signKey]

	signing {
		useGpgCmd()
		sign publishing.publications.maven
	}
}

