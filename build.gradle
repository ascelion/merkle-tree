buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath 'pl.allegro.tech.build:axion-release-plugin:1.18.+'
	}
}

apply plugin: 'pl.allegro.tech.build.axion-release'

group = 'ascelion.public'
version = scmVersion.version

allprojects {
	apply plugin: 'eclipse'

	repositories {
		mavenCentral()
	}

	defaultTasks 'clean', 'build'
}

subprojects { sub ->
	group = rootProject.group
	version = rootProject.version

	apply plugin: 'java'

	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	tasks.withType( AbstractArchiveTask ) {
		archiveVersion = ''
	}

	tasks.withType( Jar ) {
		manifest {
			attributes(
				'Built-By'       : System.properties['user.name'],
				'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Build-Branch'   : scmVersion.scmPosition.branch,
				'Build-Commit'   : scmVersion.scmPosition.revision,
				'Build-Revision' : "${scmVersion.version}-${scmVersion.scmPosition.shortRevision}",
				'Created-By'     : "Gradle ${gradle.gradleVersion}",
				'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
			)
		}
	}

	apply from: rootProject.file('tests.gradle')
}
